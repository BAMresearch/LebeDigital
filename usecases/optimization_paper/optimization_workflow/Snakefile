import fenics_concrete  # somehow required...
from lebedigital.unit_registry import ureg
import pint_pandas
import pint
import pandas as pd
import json
import numpy as np

PATH_TO_SCRIPTS = '../../../lebedigital/'


#transforms dictionary in the form "example:{"value": 1 "units": "..."}, example2:{"value": 1 "units": "..."}, ... to pint object
def from_dict_2_pint_object(dictionary):
    new_dict_pint={}
    for key in dictionary.keys():
        if dictionary[key]['unit'] == 'degree_Celsius':
            # somehow temperature is different...
            Q_ = ureg.Quantity
            new_dict_pint[key] = Q_(dictionary[key]['value'],ureg.degC)
            continue
        new_dict_pint[key] = dictionary[key]['value'] * ureg(dictionary[key]['unit'])
    return new_dict_pint

def load_json(path:str)->dict:
    if path[-5:] == '.json':
        with open(path) as f:
            data = json.load(f)
    return data

def read_pint_dicts(input):
    dict = {}
    for path in input:
        # only include .json files
        # maybe this has to change at some point...
        if path[-5:] == '.json':
            with open(path) as f:
                # TODO implement check so that parameters are not overwritten
                dict.update(json.loads(f.read()))

    return from_dict_2_pint_object(dict)


#transforms pint object to the standard dictionary form decided
def from_pint_object_2_dict(dictionary):
    new_dict={}
    for key in dictionary.keys():
        new_dict[key] = {"value": dictionary[key].magnitude, "unit":"{0.units}".format(dictionary[key])}
    return new_dict


def write_pint_dict(dict,path):
    dict=from_pint_object_2_dict(dict)
    with open(path,"w") as f:
        f.write(json.dumps(dict, indent=4))
#
def write_pint_df(pint_df,path):
    pint_df.pint.dequantify().to_csv(path, index=None)

def read_pint_df(path):
    pint_df = pd.read_csv(path, header=[0, 1])
    pint.set_application_registry(ureg)  # required to use the same registry
    return pint_df.pint.quantify(level=-1)

# this rule collects all overall targets.
# an output must be an input to be computed
# the alternative is to define them in the console command, which is suboptimal
rule workflow_targets:
    input:
        "Results/gwp_beam.json",
        "Results/kpi_from_fem.json",
        'Results/demonstrator_beam.xdmf'


# This wont work, align this with the function agruments
# rule get_mix_hydration_parameters:
#     input:
#         script = PATH_TO_SCRIPTS + 'demonstrator_scripts/dummy_hydration_parameters.py'
#     output:
#         results = 'Results/mixes_hydration_parameters.json'
#     run:
#         from lebedigital.demonstrator_scripts.dummy_hydration_parameters import dummy_hydration_parameters

#         results = {}

#         # run script
#         results['mix1_B1'], results['mix1_B2'], results['mix1_eta'], results['mix1_E_act'], results['mix1_Q_pot'], results['mix1_T_ref'] = \
#             dummy_hydration_parameters(0,42)
#         results['mix2_B1'], results['mix2_B2'], results['mix2_eta'], results['mix2_E_act'], results['mix2_Q_pot'], results['mix2_T_ref'] = \
#             dummy_hydration_parameters(1,42)

#         write_pint_dict(results,output.results)

# Updated as per the correct fucntion. Also this rule is not used for the paper (was not existing then)
rule get_mix_hydration_parameters:
    input:
        script = PATH_TO_SCRIPTS + 'demonstrator_scripts/computation_hydration_parameters.py',
        mean_NN = "Inputs/NN_model_hydration_final.pt",
        cov_params = "Inputs/cov_parameters_hydration_final.csv",
        seed = "Inputs/seed_learnt_models.json"
    output:
        results = 'Results/mixes_hydration_parameters.json'
    run:
        from lebedigital.demonstrator_scripts.computation_hydration_parameters import computation_hydration_parameters

        q = load_json(input.seed)
        results = {}

        # run script
        results['mix1_B1'], results['mix1_B2'], results['mix1_eta'], results['mix1_E_act'], results['mix1_Q_pot'], results['mix1_T_ref'] = \
            computation_hydration_parameters(0.0,input.mean_NN, input.cov_params,q['seed']['value'])
        results['mix2_B1'], results['mix2_B2'], results['mix2_eta'], results['mix2_E_act'], results['mix2_Q_pot'], results['mix2_T_ref'] = \
            computation_hydration_parameters(1.0,input.mean_NN, input.cov_params,q['seed']['value'])

        write_pint_dict(results,output.results)

# this rule is not used for the paper (was not existing then)
rule compute_doh_at_28_days:
    input:
        alpha_max = "Results/approx_max_doh.json",
        hydration_params = "Results/mixes_hydration_parameters.json"
    output:
        results = 'Results/mixes_alpha_t28d.json'
    run:
        import fenics_concrete

        p = read_pint_dicts(input)

        time_total =  60 * 60 * 24 * 28 # 28 days in seconds
        dt = 60 * 60 # 1 hour in seconds
        T = 20 # 20 degrees Celsius ambient temperature

        time_list = np.arange(0,time_total,dt)

        material_problem = fenics_concrete.ConcreteThermoMechanical()
        hydration_fkt = material_problem.get_heat_of_hydration_ftk()

        for key in p.keys():
            # check if paramter has a unit, then remove it, for now...
            if type(p[key]) == type(1 * ureg("")):
                p[key] = p[key].magnitude

        parameters1 = {'B1': p['mix1_B1'], 'B2': p['mix1_B2'], 'eta': p['mix1_eta'], 'E_act': p['mix1_E_act'], 'Q_pot': p['mix1_Q_pot'], 'T_ref': p['mix1_T_ref'],'alpha_max': p['max_degree_of_hydration']}
        heat_list1, doh_list1 = hydration_fkt(T,time_list,dt,parameters1)

        parameters2 = {'B1': p['mix2_B1'], 'B2': p['mix2_B2'], 'eta': p['mix2_eta'], 'E_act': p['mix2_E_act'], 'Q_pot': p['mix2_Q_pot'], 'T_ref': p['mix2_T_ref'],'alpha_max': p['max_degree_of_hydration']}
        heat_list2, doh_list2 = hydration_fkt(T,time_list,dt,parameters2)

        results = {}
        # run script
        results["alpha_mix1"] = float(doh_list1[-1]) * ureg.dimensionless
        results["alpha_mix2"] = float(doh_list2[-1]) * ureg.dimensionless

        write_pint_dict(results,output.results)

rule kpi_from_fem:
    input:
        script = PATH_TO_SCRIPTS + 'demonstrator_scripts/kpi_from_fem.py',
        temperature_limit = "Inputs/fem_limits.json",
        fem_model = "Results/fem_model.csv",

    output:
        results = "Results/kpi_from_fem.json"

    run:
        from lebedigital.demonstrator_scripts.kpi_from_fem import kpi_from_fem
        p = read_pint_dicts(input)
        df = read_pint_df(input.fem_model)


        # run script
        results = kpi_from_fem(df,p['temperature_limit'],p['time_limit'])

        write_pint_dict(results,output.results)


# TODO, add/write respective FEM model for beam...
rule fem_model:
    input:
        script = PATH_TO_SCRIPTS + 'simulation/precast_column.py',
        geometry = "Inputs/geometry.json",
        steel_yield = 'Inputs/steel_properties.json',
        fem_control = "Inputs/fem_control.json",
        fem_parameters = "Inputs/fem_parameters.json",
        results_homogenization = "Results/homogenization.json",
        results_approx_max_doh = "Results/approx_max_doh.json",
        results_approx_hydration_parameters = 'Results/approx_hydration_parameters.json',
        results_alpha_t28d = 'Results/alpha_t28d.json'

    output:
        pint_results = "Results/fem_model.csv",
        paraview = 'Results/demonstrator_beam.xdmf'

    run:
        from lebedigital.simulation.demonstrator_beam import demonstrator_beam

        # get parameters
        p = read_pint_dicts(input)

        # fixing name discrepancies from other rules or inputs
        p['alpha_tx'] = p.pop('alpha_t28d')
        p['vol_heat_cap'] = p.pop('C')
        p['density'] = p.pop('rho')
        p['themal_cond'] = p.pop('kappa')
        p['Q_inf'] = p.pop('Q')
        p['alpha_max'] = p.pop('max_degree_of_hydration')
        p['ft'] = p.pop('steel_yield')  # setting the tensile yield to steel as a test, only used in yield fct

        results = {}
        # run script
        # output is a pandas-pint dataframe
        df_pint =  demonstrator_beam(p['full_time'], p['time_step'],p,
                                     pv_output=True, pv_name='Results/demonstrator_beam')

        # writing the data frame
        write_pint_df(df_pint, output.pint_results)


rule approx_max_doh:
    input:
        script = PATH_TO_SCRIPTS + 'demonstrator_scripts/approximate_max_degree_of_hydration.py',
        wb_ratio = 'Inputs/wb_ratio.json'

    output:
        results = "Results/approx_max_doh.json"

    run:
        from lebedigital.demonstrator_scripts.approximate_max_degree_of_hydration import approximate_max_degree_of_hydration
         #merging contents of both dictionaries and individual variable inputs

        p = read_pint_dicts(input)

        results = {}
        # run script
        results["max_degree_of_hydration"] =  approximate_max_degree_of_hydration(p['wb_mass_ratio'])

        write_pint_dict(results,output.results)

rule interpolate_alpha_t28d:
    input:
        script = PATH_TO_SCRIPTS + 'demonstrator_scripts/interpolate_alpha_t28d.py',
        sc_fraction = 'Results/mix_volume_contents.json',
        alpha_t28d = 'Results/mixes_alpha_t28d.json'

    output:
        results = "Results/alpha_t28d.json"

    run:
        from lebedigital.demonstrator_scripts.interpolate_alpha_t28d import interpolate_alpha_t28d
         #merging contents of both dictionaries and individual variable inputs

        p = read_pint_dicts(input)

        results = {}
        # run script
        results["alpha_t28d"] =  interpolate_alpha_t28d(p['alpha_mix1'], p['alpha_mix2'], p['sc_volume_fraction'])

        write_pint_dict(results,output.results)


rule compute_loads:
    input:
        script = PATH_TO_SCRIPTS + 'demonstrator_scripts/computation_loads_with_safety.py',
        loads = 'Inputs/loads.json'

    output:
        results = "Results/loads_safe.json"

    run:
        from lebedigital.demonstrator_scripts.computation_loads_with_safety import computation_loads_with_safety
         #merging contents of both dictionaries and individual variable inputs

        p = read_pint_dicts(input)

        results = {}
        # run script
        results["distributed_load"], results["point_load"] =  computation_loads_with_safety(p['safety_factor_permanent'],
            p['safety_factor_variable'], p['distributed_load_permanent'], p['distributed_load_variable'],
            p['point_load_permanent'],p['point_load_variable'])

        write_pint_dict(results,output.results)



rule approx_tensile_strength:
    input:
        script = PATH_TO_SCRIPTS + 'demonstrator_scripts/approximate_tensile_strength.py',
        results_homogenization = "Results/homogenization.json",

    output:
        results = "Results/approx_tensile_strength.json"

    run:
        from lebedigital.demonstrator_scripts.approximate_tensile_strength import approximate_tensile_strength
         #merging contents of both dictionaries and individual variable inputs

        p = read_pint_dicts(input)

        results = {}
        # run script
        results["concrete_ft"] =  approximate_tensile_strength(p['fc'])

        write_pint_dict(results,output.results)


rule beam_design:
    input:
        script = PATH_TO_SCRIPTS + 'demonstrator_scripts/beam_design.py',
        geometry = "Inputs/geometry.json",
        steel_yield = 'Inputs/steel_properties.json',
        beam_design = "Inputs/beam_design.json",
        loads = "Results/loads_safe.json",
        results_homogenization = "Results/homogenization.json",

    output:
        results = "Results/beam_design.json"

    run:
        from lebedigital.demonstrator_scripts.beam_design import check_beam_design
         #merging contents of both dictionaries and individual variable inputs

        p = read_pint_dicts(input)


        results = {}
        # run script
        beam_design =  check_beam_design(span=p['length'],
                                                         width=p['width'],
                                                         height=p['height'],
                                                         point_load=p["point_load"],
                                                         distributed_load=p["distributed_load"],
                                                         compr_str_concrete=p["fc"],
                                                         yield_str_steel=p['steel_yield'],
                                                         steel_dia_bu=p['stirrups_diameter'],
                                                         cover_min=p['concrete_cover'])
        results["n_reinforcement"] = beam_design["n_steel_bars"]
        results["reinforcement_diameter"] = beam_design["diameter"]
        results["constraint_beam_design"] = beam_design["constraint_beam_design"]

        write_pint_dict(results,output.results)

rule gwp_steel_per_volume:
    input:
        script = PATH_TO_SCRIPTS + 'demonstrator_scripts/computation_GWP_steel_per_volume.py',
        steel_properties = "Inputs/steel_properties.json"

    output:
        results = "Results/steel_gwp_per_volume.json"

    run:
        from lebedigital.demonstrator_scripts.computation_GWP_steel_per_volume import computation_GWP_steel_per_volume
         #merging contents of both dictionaries and individual variable inputs
        p = read_pint_dicts(input)

        results = {}
        # run script
        results["steel_gwp_per_volume"] = computation_GWP_steel_per_volume(p['gwp_steel'], p['density_steel'])

        write_pint_dict(results,output.results)


rule gwp_beam:
    input:
        script = PATH_TO_SCRIPTS + 'demonstrator_scripts/computation_GWP_per_part.py',
        geometry = "Inputs/geometry.json",
        steel_gwp = "Results/steel_gwp_per_volume.json",
        results_gwp_mix = "Results/gwp_mix.json",
        results_beam_design = "Results/beam_design.json",

    output:
        results = "Results/gwp_beam.json"

    run:
        from lebedigital.demonstrator_scripts.computation_GWP_per_part import computation_GWP_per_part
         #merging contents of both dictionaries and individual variable inputs

        p = read_pint_dicts(input)

        results = {}
        # run script
        results["gwp_beam"] = computation_GWP_per_part(p['gwp_mix'], p['steel_gwp_per_volume'],
                                                       p['width'], p['height'], p['length'],
                                                       p['n_reinforcement'],p['reinforcement_diameter'])

        write_pint_dict(results,output.results)


rule gwp_mix:
    input:
        script = PATH_TO_SCRIPTS + 'demonstrator_scripts/computation_GWP_mix.py',
        material_properties = "Inputs/material_properties.json",
        results_mix_volume_contents = 'Results/mix_volume_contents.json',

    output:
        results = "Results/gwp_mix.json"

    run:
        from lebedigital.demonstrator_scripts.computation_GWP_mix import computation_GWP_mix
         #merging contents of both dictionaries and individual variable inputs

        p = read_pint_dicts(input)

        constituents = {'cement' : {'content': p['cem_mass_per_cubic_meter_concrete'], 'GWP': p['gwp_cement']},
                        'slag' : {'content': p['sub_mass_per_cubic_meter_concrete'], 'GWP': p['gwp_slag']},
                        'water' : {'content': p['water_mass_per_cubic_meter_concrete'], 'GWP': p['gwp_water']},
                        'aggregates' : {'content': p['aggregates_mass_per_cubic_meter_concrete'], 'GWP': p['gwp_aggregates']}
                        }

        results = {}
        # run script
        results["gwp_mix"] = computation_GWP_mix(constituents)

        write_pint_dict(results,output.results)


# rule approx_paste_properties:
#     # the stiffness and strength of the cement paste is estimated and interpolated
#     input:
#         script = PATH_TO_SCRIPTS + 'demonstrator_scripts/computation_paste_strength_stiffness.py',
#         sc_fraction = 'Results/mix_volume_contents.json',
#         phi_paste = "Inputs/phi_paste.json"

#     output:
#         results = "Results/approx_paste_properties.json"

#     run:
#         from lebedigital.demonstrator_scripts.computation_paste_strength_stiffness import computation_paste_strength_stiffness
#          #merging contents of both dictionaries and individual variable inputs

#         p = read_pint_dicts(input)
#         q = load_json(input.phi_paste)
#         results = {}
#         # run script
#         results["paste_E"], results["paste_fc"] = computation_paste_strength_stiffness(p['sc_volume_fraction'],
#         q['phi_mean']['value'], q['phi_cov']['value'],q['seed']['value'])

#         write_pint_dict(results,output.results)

rule approx_paste_properties:
    # the stiffness and strength of the cement paste is estimated and interpolated
    # to just check this, run : snakemake --core 1 approx_paste_properties --use-conda
    input:
        script = PATH_TO_SCRIPTS + 'demonstrator_scripts/computation_paste_strength_stiffness.py',
        sc_fraction = 'Results/mix_volume_contents.json',
        mean_NN = "Inputs/NN_model_homogenization_final.pt",
        cov_params = "Inputs/cov_parameters_homogenization_final.csv",
        seed = "Inputs/seed_learnt_models.json"
    output:
        results = "Results/approx_paste_properties.json"

    run:
        from lebedigital.demonstrator_scripts.computation_paste_strength_stiffness import computation_paste_strength_stiffness
         #merging contents of both dictionaries and individual variable inputs

        p = load_json(input.sc_fraction)
        q = load_json(input.seed)
        results = {}
        # run script
        results["paste_E"], results["paste_fc"] = computation_paste_strength_stiffness(p['sc_volume_fraction']['value'],
        input.mean_NN, input.cov_params,q['seed']['value'])
        print(results)
        write_pint_dict(results,output.results)


# rule approx_hydration_parameters:
#     # the hydration parameters are approximated based on slag content
#     input:
#         script = PATH_TO_SCRIPTS + 'demonstrator_scripts/dummy_hydration_parameters.py',
#         sc_fraction = 'Results/mix_volume_contents.json',
#         phi_hydration = 'Inputs/phi_hydration.json'

#     output:
#         results = 'Results/approx_hydration_parameters.json'

#     run:
#         from lebedigital.demonstrator_scripts.dummy_hydration_parameters import dummy_hydration_parameters
#          #merging contents of both dictionaries and individual variable inputs

#         # had to bypass this pint thing as it was not letting me use numpy.
#         p = read_pint_dicts(input)
#         q = load_json(input.phi_hydration)
#         s = load_json(input.sc_fraction)
#         results = {}

#         # run script
#         results['B1'], results['B2'], results['eta'], results['E_act'], results['Q_pot'], results['T_ref'] = \
#             dummy_hydration_parameters(s['sc_volume_fraction']['value'],q['phi_mean']['value'],q['phi_cov']['value'],q['seed']['value'])

#         write_pint_dict(results,output.results)

rule approx_hydration_parameters:
    # the hydration parameters are approximated based on slag content
    input:
        script = PATH_TO_SCRIPTS + 'demonstrator_scripts/computation_hydration_parameters.py',
        sc_fraction = 'Results/mix_volume_contents.json',
        mean_NN = "Inputs/NN_model_hydration_final.pt",
        cov_params = "Inputs/cov_parameters_hydration_final.csv",
        seed = "Inputs/seed_learnt_models.json"

    output:
        results = 'Results/approx_hydration_parameters.json'

    run:
        from lebedigital.demonstrator_scripts.computation_hydration_parameters import computation_hydration_parameters
         #merging contents of both dictionaries and individual variable inputs

        p = load_json(input.sc_fraction)
        q = load_json(input.seed)
        results = {}

        # run script
        results['B1'], results['B2'], results['eta'], results['E_act'], results['Q_pot'], results['T_ref'] = \
            computation_hydration_parameters(p['sc_volume_fraction']['value'],input.mean_NN, input.cov_params,q['seed']['value'])
        print(results)

        write_pint_dict(results,output.results)


rule mix_volume_contents:
    input:
        script = PATH_TO_SCRIPTS + 'demonstrator_scripts/computation_volume_content.py',
        material_properties = "Inputs/material_properties.json",
        aggregates_volume_fraction = 'Inputs/aggregates_volume_fraction.json',
        sc_fraction = "Inputs/sc_fraction.json",
        wb_ratio = 'Inputs/wb_ratio.json',


    output:
        results = 'Results/mix_volume_contents.json'

    run:
        from lebedigital.demonstrator_scripts.computation_volume_content import computation_volume_content
         #merging contents of both dictionaries and individual variable inputs

        p = read_pint_dicts(input)

        # run script
        # running and storing the results
        results = computation_volume_content(p)

        write_pint_dict(results,output.results)


rule specific_heat_capacity_paste:
    input:
        script = PATH_TO_SCRIPTS + 'demonstrator_scripts/computation_specific_heat_capacity_paste.py',
        volume_contents = "Results/mix_volume_contents.json",
        material_properties = "Inputs/material_properties.json"

    output:
        results = "Results/specific_heat_capacity_paste.json"

    run:
        from lebedigital.demonstrator_scripts.computation_specific_heat_capacity_paste import computation_specific_heat_capacity_paste
         #merging contents of both dictionaries and individual variable inputs
        p = read_pint_dicts(input)

        results = {}
        # run script
        results["paste_C"] = computation_specific_heat_capacity_paste(p['cem_vol_fraction'],
                                                                      p['sub_vol_fraction'],
                                                                      p['water_vol_fraction'],
                                                                      p['cement_C'],
                                                                      p['sub_c'],
                                                                      p['water_C'],
                                                                      p['density_cem'],
                                                                      p['density_sub'],
                                                                      p['density_water'])

        write_pint_dict(results,output.results)




rule homogenization:
    input:
        script = PATH_TO_SCRIPTS + 'simulation/concrete_homogenization.py',
        material_properties = "Inputs/material_properties.json",
        specific_heat_capacity_paste = "Results/specific_heat_capacity_paste.json",
        results_mix_volume_contents = 'Results/mix_volume_contents.json',
        aggregates_volume_fraction = 'Inputs/aggregates_volume_fraction.json',
        results_approx_hydration_parameters = 'Results/approx_hydration_parameters.json',
        results_approx_paste_properties = 'Results/approx_paste_properties.json',

    output:
        results = "Results/homogenization.json"

    run:
        from lebedigital.simulation.concrete_homogenization import concrete_homogenization

        p = read_pint_dicts(input)

        # fixing name discrepancies from other rules or inputs
        p['paste_Q'] = p.pop('Q_pot')
        p['paste_rho'] = p.pop('density_paste')
        p['aggregates_rho'] = p.pop('density_aggregates')
        p['aggregates_vol_frac'] = p.pop('aggregates_volume_fraction')

        results = concrete_homogenization(p)

        write_pint_dict(results, output.results)